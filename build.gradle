ext {
	gradleVersion = '3.0'
	javaVersion = '1.8'
	splineVersion = '0.1'
    springPlattformBomVersion = '2.0.7.RELEASE'
    axonVersion = '2.4.5'
    springfoxVersion = '2.6.0'
    katharsisVersion = '2.6.0'
    reflectionsVersion = '0.9.10'
    springNeo4jVersion = '4.1.4.RELEASE'
    lombokVersion = '1.16.10'
    mongoDriverVersion = '2.9.3'
}

buildscript {
	ext {
		springBootVersion = '1.4.1.RELEASE'
		dependencyManagementVersion = '0.6.1.RELEASE'
	}
	repositories {
		maven {
			url 'http://maven.springframework.org/release'
		}
        jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath "io.spring.gradle:dependency-management-plugin:$dependencyManagementVersion"
	}
}

configure(allprojects) {
	repositories {
		maven {
			url 'http://maven.springframework.org/release'
		}
		jcenter()
	}
	group='se.spline'
	version=splineVersion
}

configure(subprojects) {
	apply plugin: 'java'

	compileJava {
		sourceCompatibility = javaVersion
		targetCompatibility = javaVersion
	}

	compileTestJava {
		sourceCompatibility = javaVersion
		targetCompatibility = javaVersion
	}

    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
            mavenBom "io.spring.platform:platform-bom:$springPlattformBomVersion"
        }
        dependencies {
            dependencySet (group: 'org.axonframework', version: axonVersion) {
                entry 'axon-core'
                entry 'axon-test'
                entry 'axon-mongo'
            }
            dependencySet (group: 'io.springfox', version: springfoxVersion) {
                entry 'springfox-swagger2'
                entry 'springfox-swagger-ui'
            }
            dependency group: 'org.projectlombok', name: 'lombok', version: lombokVersion
            dependency group: 'io.katharsis', name: 'katharsis-spring', version: katharsisVersion
            dependency group: 'org.reflections', name: 'reflections', version: reflectionsVersion
            dependency group: 'org.springframework.data', name: 'spring-data-neo4j', version: springNeo4jVersion
            dependency group: 'org.mongodb', name: 'mongo-java-driver', version: mongoDriverVersion
        }
    }
}

task wrapper(type: Wrapper) {
	gradleVersion = "${project.ext.gradleVersion}"
}


